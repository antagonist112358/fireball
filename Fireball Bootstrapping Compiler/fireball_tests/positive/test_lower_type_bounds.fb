/* Test generic type variable bounds */
namespace Fireball.Tests
{
	using System  
	using System.Collections
	using System.Collections.Generic

	// Some test types
	class Super { }
	class Child1 extends Super { }
	class Child2 extends Super { }
	class SubChild1 extends Child1 { }
	
	// Test lower-type bounds
	trait ListNode[+T] extends IEnumerable[T]
	{
		def Head : T
		def Tail : ListNode[T]
		def Prepend[U :> T](item : U) : ListNode[U]
	}

	module DefaultListNode[T] {
	
		private class EmptyListNode extends ListNode[T]
		{
			def Head : T
				with get { throw NotImplementedException() }
			
			def Tail : ListNode[T]
				with get { throw NotImplementedException() }
					
			def Prepend[U :> T](item : U) : ListNode[U] { throw NotImplementedException() }
			def GetEnumerator() : IEnumerator[T] { throw NotImplementedException() }
			def GetNonGenericEnumerator() : IEnumerator implements IEnumerable.GetEnumerator { throw NotImplementedException() }
		}
		
		public val Empty : ListNode[T] = new EmptyListNode()       
	}
	
	
	class RealListNode[T](head : T, tail : ListNode[T] = DefaultListNode[T].Empty) extends ListNode[T] {
		
		private class RealListNodeEnumerator[T2](node : ListNode[T2]) extends IEnumerator[T2] {
			var currentNode : ListNode[T2] = node
			var hasMovedOnce : bool = false
			
			def MoveNext() : bool {
				if (!hasMovedOnce)
					hasMovedOnce = true
				else 
					currentNode = currentNode.Tail
				
				if (object.ReferenceEquals(currentNode, DefaultListNode[T2].Empty)) false
				else true
			}		
      
			def Current : T2
			with get {
				currentNode.Head
			}
			
			def Dispose() : void { }
			def Reset() : void { }
		}
		
		def Head : T = head
		def Tail : ListNode[T] = tail
		
		def Prepend[U :> T](item : U) : ListNode[U]
		{
			def convertRecursive(inListNode : ListNode[T]) : ListNode[U] {
				if(inListNode == DefaultListNode[T].Empty)
					new RealListNode(head :> U, convertRecursive(inListNode.Tail))
				else
					new RealListNode(head :> U, DefaultListNode[U].Empty)
			}
			
			new RealListNode(item, convertRecursive(this))
		}
		
		def GetEnumerator() : IEnumerator[T] { RealListNodeEnumerator(this) }
		def GetNonGenericEnumerator() : IEnumerator implements IEnumerable.GetEnumerator {
			new RealListNodeEnumerator(this)
		}
				
	}
		
	// Test upper-type bounds
	public trait Container[T]
	{
		def Value : T
		def Replace[U :> T](item : U) : Container[U]
	}
	
	class RealContainer[T](item : T) extends Container[T] {
		def Value : T with get { item }
		def Replace[U :> T](newItem : U) : Container[U] { RealContainer(newItem) }
	}
	
	class Stringable {
		override def ToString() : string {
			"Lower-bound casting finally works!"
		}
	}
  	
	public module Program
	{
		private type TestType = Array[String]    
    
		def Main(args : Array[String]) : void {
			val aList = new RealListNode(Stringable())
			val locationName = "World"
			val bList = aList.Prepend($"Hello $locationName")

			foreach(item in bList) {
				Console.WriteLine(item)
			}			
      
			val subChild = new SubChild1()
			val cont = new RealContainer(Child1())
			val cont2 = cont.Replace(subChild)			     
      
			assert(cont2.Value == subChild)
		}
	}
}
/*
BEGIN-OUTPUT
Hello World
Lower-bound casting finally works!
END-OUTPUT
*/